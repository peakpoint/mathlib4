/-
Copyright (c) 2024 Antoine Chambert-Loir, Mar√≠a In√©s de Frutos Fern√°ndez. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro, Antoine Chambert-Loir, Mar√≠a In√©s de Frutos Fern√°ndez
-/
import Mathlib.Topology.UniformSpace.Cauchy
import Mathlib.Topology.Algebra.IsUniformGroup.Defs

/-! # Discrete uniformity

The discrete uniformity is the smallest possible uniformity, the one for which
the diagonal is an entourage of itself.

It induces the discrete topology.

It is complete.

-/

open Filter UniformSpace

/-- The discrete uniformity -/
@[mk_iff discreteUniformity_iff_eq_bot]
class DiscreteUniformity (X : Type*) [u : UniformSpace X] : Prop where
  eq_bot : u = ‚ä•

namespace DiscreteUniformity

/-- The bot uniformity is the discrete uniformity. -/
instance (X : Type*) : @DiscreteUniformity X ‚ä• :=
  @DiscreteUniformity.mk X ‚ä• rfl

variable (X : Type*) [u : UniformSpace X] [DiscreteUniformity X]

theorem _root_.discreteUniformity_iff_eq_principal_relId {X : Type*} [UniformSpace X] :
    DiscreteUniformity X ‚Üî uniformity X = ùìü Rel.id := by
  rw [discreteUniformity_iff_eq_bot, UniformSpace.ext_iff, Filter.ext_iff, bot_uniformity]

@[deprecated (since := "2025-07-11")]
alias discreteUniformity_iff_eq_principal_idRel := discreteUniformity_iff_eq_principal_relId

theorem eq_principal_relId : uniformity X = ùìü Rel.id :=
  discreteUniformity_iff_eq_principal_relId.mp inferInstance

@[deprecated (since := "2025-07-11")]
alias eq_principal_idRel := eq_principal_relId

/-- The discrete uniformity induces the discrete topology. -/
instance : DiscreteTopology X where
  eq_bot := by
    rw [DiscreteUniformity.eq_bot (X := X), UniformSpace.toTopologicalSpace_bot]

theorem _root_.discreteUniformity_iff_relId_mem_uniformity {X : Type*} [UniformSpace X] :
    DiscreteUniformity X ‚Üî Rel.id ‚àà uniformity X := by
  rw [‚Üê uniformSpace_eq_bot, discreteUniformity_iff_eq_bot]

@[deprecated (since := "2025-07-11")]
alias discreteUniformity_iff_idRel_mem_uniformity := discreteUniformity_iff_relId_mem_uniformity

theorem relId_mem_uniformity : Rel.id ‚àà uniformity X :=
  discreteUniformity_iff_relId_mem_uniformity.mp inferInstance

@[deprecated (since := "2025-07-11")]
alias idRel_mem_uniformity := relId_mem_uniformity

variable {X} in
/-- A product of spaces with discrete uniformity has a discrete uniformity. -/
instance {Y : Type*} [UniformSpace Y] [DiscreteUniformity Y] :
    DiscreteUniformity (X √ó Y) := by
  simp [discreteUniformity_iff_eq_principal_relId, uniformity_prod_eq_comap_prod,
    eq_principal_relId, Rel.id, Set.prod_eq, Prod.ext_iff, Set.setOf_and]

variable {x} in
/-- On a space with a discrete uniformity, any function is uniformly continuous. -/
theorem uniformContinuous {Y : Type*} [UniformSpace Y] (f : X ‚Üí Y) :
    UniformContinuous f := by
  simp only [uniformContinuous_iff, DiscreteUniformity.eq_bot, bot_le]

/-- The discrete uniformity makes a group a `IsUniformGroup. -/
@[to_additive "The discrete uniformity makes an additive group a `IsUniformAddGroup`."]
instance [Group X] : IsUniformGroup X where
  uniformContinuous_div := uniformContinuous (X √ó X) fun p ‚Ü¶ p.1 / p.2

variable {X} in
/-- A Cauchy filter in a discrete uniform space is contained in the principal filter
of a point. -/
theorem eq_pure_of_cauchy {Œ± : Filter X} (hŒ± : Cauchy Œ±) : ‚àÉ x : X, Œ± = pure x := by
  rcases hŒ± with ‚ü®Œ±_ne_bot, Œ±_le‚ü©
  simp only [DiscreteUniformity.eq_principal_relId, le_principal_iff, mem_prod_iff] at Œ±_le
  obtain ‚ü®S, hS, T, hT, H‚ü© := Œ±_le
  obtain ‚ü®x, rfl, _‚ü© := Rel.exists_eq_singleton_of_prod_subset_id (Œ±_ne_bot.nonempty_of_mem hS)
    (Filter.nonempty_of_mem hT) H
  exact ‚ü®x, Œ±_ne_bot.le_pure_iff.mp <| le_pure_iff.mpr hS‚ü©

@[deprecated (since := "2025-03-23")]
alias _root_.UniformSpace.DiscreteUnif.cauchy_le_pure := eq_pure_of_cauchy

/-- The discrete uniformity makes a space complete. -/
instance : CompleteSpace X where
  complete {f} hf := by
    obtain ‚ü®x, rfl‚ü© := eq_pure_of_cauchy hf
    exact ‚ü®x, pure_le_nhds x‚ü©

variable {X}

/-- A constant to which a Cauchy filter in a discrete uniform space converges. -/
noncomputable def cauchyConst {Œ± : Filter X} (hŒ± : Cauchy Œ±) : X :=
  (eq_pure_of_cauchy hŒ±).choose

@[deprecated (since := "2025-03-23")]
alias _root_.UniformSpace.DiscreteUnif.cauchyConst := cauchyConst

theorem eq_pure_cauchyConst {Œ± : Filter X} (hŒ± : Cauchy Œ±) : Œ± = pure (cauchyConst hŒ±) :=
  (eq_pure_of_cauchy hŒ±).choose_spec

@[deprecated (since := "2025-03-23")]
alias _root_.UniformSpace.DiscreteUnif.eq_const_of_cauchy := eq_pure_cauchyConst

end DiscreteUniformity
